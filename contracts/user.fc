#include "imports/stdlib.fc";
#include "constants/constants.fc";
#include "constants/errors.fc";
#include "constants/op-codes.fc";
#include "constants/fees.fc";
#include "logic/messages.fc";

global int storage::init?;
global int storage::index;
global slice storage::master_address;
global slice storage::user_address;
global int storage::revoked_at;
global cell storage::content;

() load_data() impure {
    slice ds = get_data().begin_parse();

    storage::index = ds~load_uint(64);
    storage::master_address = ds~load_msg_addr();

    if(slice_bits(ds) > 0) {
        storage::init? = true;
        storage::user_address = ds~load_msg_addr();
        storage::revoked_at = ds~load_uint(32);
        storage::content = ds~load_ref();
    } else {
        storage::init? = false;
        storage::user_address = addr_none;
        storage::revoked_at = 0;
        storage::content = null();
    }

    ds.end_parse();
}

() save_data() impure {
    set_data(begin_cell()
        .store_uint(storage::index, 64)
        .store_slice(storage::master_address)
        .store_slice(storage::user_address)
        .store_uint(storage::revoked_at, 32)
        .store_ref(storage::content)
    .end_cell()
    );
}

() recv_internal(int my_balance, int msg_value, cell in_msg_full, slice in_msg_body) impure {
    slice cs = in_msg_full.begin_parse();
    int flags = cs~load_uint(4);
    slice sender_address = cs~load_msg_addr();
    ;; support bounce?

    load_data();
    if (~ storage::init?) {
        throw_unless(error:unauthorized, equal_slices(sender_address, storage::master_address));
        storage::user_address = in_msg_body~load_msg_addr();
        storage::content = in_msg_body~load_dict();
        storage::revoked_at = 1;
        save_data();
        return ();
    }

    int op = in_msg_body~load_uint(32);
    int query_id = in_msg_body~load_uint(64);


    ;; --------------- From User ---------------

    if(equal_slices(sender_address, storage::user_address)) {
        throw_unless(error:unauthorized, storage::revoked_at == 0);

        if (op == op::create_order) {
            (cell is_user?, _) = storage::content.udict_get_ref?(256, "is_user"H);
            throw_unless(error::not_freelancer, is_user?.begin_parse().preload_int(1));
            cell content = in_msg_body~load_dict();
            int price = in_msg_body~load_coins();
            int deadline = in_msg_body~load_uint(32);
            int time_for_check = in_msg_body~load_uint(32);
            in_msg_body.end_parse();
            cell body = begin_cell()
                .store_uint(op::create_order_master, 32)
                .store_uint(query_id, 64)
                .store_uint(storage::index, 64)
                .store_dict(content)
                .store_slice(storage::user_address)
                .store_coins(price)
                .store_uint(deadline, 32)
                .store_uint(time_for_check, 32)
                .end_cell();

            messages::send_simple_bounceable(storage::master_address, 0, null(), body, 64);
            return ();
        }

        if (op == op::add_response_user) {
            (cell is_freelancer?, _) = storage::content.udict_get_ref?(256, "is_freelancer"H);
            throw_unless(error::not_freelancer, is_freelancer?.begin_parse().preload_int(1));
            int order_id = in_msg_body~load_uint(64);
            cell content = in_msg_body~load_ref();
            in_msg_body.end_parse();

            cell body = begin_cell()
                .store_uint(op::add_response_master, 32)
                .store_uint(query_id, 64)
                .store_uint(storage::index, 64)
                .store_uint(order_id, 64)
                .store_slice(storage::user_address)
                .store_ref(content)
                .end_cell();
            messages::send_simple_bounceable(storage::master_address, 0, null(), body, 64);
            return ();
        }

        if (op == op::change_content_user) {
            cell new_content = in_msg_body~load_ref();
            in_msg_body.end_parse();
            storage::content = new_content;
            storage::revoked_at = 1;
            save_data();
            return ();
        }

        throw(0xFFFF);
    }


    ;; --------------- From Master ---------------

    if(equal_slices(sender_address, storage::master_address)) {
        if (op == op::activate_user) {
;;            throw_unless(error:unauthorized, storage::revoked_at == 1);
            storage::revoked_at = 0;
            save_data();
            return ();
        }

        if (op == op::revoke_user) {
            throw_unless(error::already_revoked, storage::revoked_at == 0);
            storage::revoked_at = now();
            save_data();
            return ();
        }

        throw(0xFFFF);
    }

    throw(error:unauthorized);
}

(int, int, slice, slice, int, cell) get_user_data() method_id {
    load_data();
    return (storage::init?, storage::index, storage::master_address, storage::user_address, storage::revoked_at, storage::content);
}